pluginManagement {
    def flutterSdkPath = {
        def localPropertiesFile = file("local.properties")
        def properties = new Properties()
        if (localPropertiesFile.exists()) {
            localPropertiesFile.withInputStream { properties.load(it) }
            return properties.getProperty("flutter.sdk")
        }
        // CI environment: use FLUTTER_ROOT env var
        def flutterRoot = System.getenv("FLUTTER_ROOT")
        if (flutterRoot != null) {
            return flutterRoot
        }
        // Fallback: flutter command location
        def result = "which flutter".execute().text.trim()
        if (result) {
            return new File(result).parentFile.parentFile.absolutePath
        }
        throw new GradleException("flutter.sdk not found")
    }
    settings.ext.flutterSdkPath = flutterSdkPath()

    includeBuild("${settings.ext.flutterSdkPath}/packages/flutter_tools/gradle")

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.1.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.0" apply false
}

include ":app"
